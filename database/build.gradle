plugins {
	id 'java'
	id 'org.liquibase.gradle' version '2.2.0'
	id("maven-publish")
}


repositories {
	mavenCentral()
}

dependencies {
	liquibaseRuntime "org.liquibase:liquibase-core:4.3.5"
	liquibaseRuntime 'info.picocli:picocli:4.6.1'
	liquibaseRuntime 'org.postgresql:postgresql:42.2.8'
}


jar {
	archiveClassifier.set("database")
	manifest {
		attributes('Implementation-Title' : 'organizationsdb')
	}

}

configurations {
	database
}

artifacts {
	database jar
}


task deployNew() {
	doLast{ liquibase.runList = "applySnapshotActivity, applyChangesActivity" }
}
deployNew.finalizedBy update

task deployChanges() {
	doLast { liquibase.runList = "applyChangesActivity" }
}
deployChanges.finalizedBy update


liquibase {

	Properties jdbcProps = new Properties()
	File jdbcPropertiesFile = project.file('jdbc.properties')
	if (!jdbcPropertiesFile.exists()) {
		jdbcProps.setProperty("jdbc.url", "")
		jdbcProps.setProperty("jdbc.username", "")
		jdbcProps.setProperty("jdbc.password", "")
		jdbcProps.setProperty("jdbc.driverClassName", "")
	} else {
		jdbcProps.load(jdbcPropertiesFile.newDataInputStream())
	}
	
	System.getProperties().put("tblspace_data", tblspace_data)
	System.getProperties().put("tblspace_data_idx",  tblspace_data_idx)
	System.getProperties().put("tblspace_work",  tblspace_work)
	System.getProperties().put("tblspace_work_idx",  tblspace_work_idx)

	activities {

		noChangeLogActivity {
			url jdbcProps.get("jdbc.url")
			username jdbcProps.get("jdbc.username")
			password jdbcProps.get("jdbc.password")
		}

		applySnapshotActivity {
			changeLogFile '/src/main/resources/organization/snapshot/changelog.xml'
			url jdbcProps.get("jdbc.url")
			username jdbcProps.get("jdbc.username")
			password jdbcProps.get("jdbc.password")
		}

		applyChangesActivity {
			changeLogFile '/src/main/resources/organization/changes/changelog.xml'
			url jdbcProps.get("jdbc.url")
			username jdbcProps.get("jdbc.username")
			password jdbcProps.get("jdbc.password")
		}
	}

	try {
		runList = project.ext.runList
	} catch (Exception e) {
		runList = 'noChangeLogActivity'
	}
}



publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = uri("https://maven.pkg.github.com/onbelayconsulting/organizations")
			credentials {
				username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
				password = project.findProperty("gpr.key") ?: System.getenv("GPR_TOKEN")
			}
		}
	}
	publications {
		organizationdblibrary(MavenPublication) {
			groupId = 'com.onbelay.organization'
			artifactId = 'organizationdb'
			version = System.getenv("SEMANTIC_VERSION") ?: project.findProperty("semantic_version")
			artifact jar
		}
	}
}
